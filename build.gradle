plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'    
}

group 'hu.elte'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.2"
}

checkstyle {
    toolVersion = "8.11"
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
    afterEvaluate {
        classDirectories = files(
                classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            '**/view/**',
                            '**/Application**'
                    ])
                })
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
            excludes = [
                    '**.view.**',
                    '**.Application'
            ]
        }
    }
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

test {
    useJUnitPlatform()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.easytesting', name: 'fest-swing', version: '1.2.1'
}

jar {
    manifest {
        attributes "Main-Class": "hu.elte.pos.Application"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}